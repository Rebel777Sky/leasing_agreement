
#Область ПрограммныйИнтерфейс

Процедура ПолучитьЛизинговыеДвижения(ПараметрыОтбора = Неопределено ) Экспорт

	// { Михалев Д.А. #00001#17.01.2026#
	Если ПараметрыОтбора = Неопределено Тогда
		ПараметрыОтбора = ПолучитьПараметрыОтбора();
	КонецЕсли;
	
	ВыполнитьЗапросЛизинговыеДвижения(ПараметрыОтбора);
	// } Михалев Д.А. #00001#17.01.2026#

КонецПроцедуры

Функция ПолучитьДоговор(Параметры) Экспорт

	// { Михалев Д.А. #00001#17.01.2026#
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ДоговорыАрендыСпр.Ссылка КАК Договор
		|ИЗ
		|	Справочник.ДоговорыАренды КАК ДоговорыАрендыСпр
		|ГДЕ
		|	ДоговорыАрендыСпр.Организация = &Организация
		|	И ДоговорыАрендыСпр.Контрагент = &Контрагент
		|	И ДоговорыАрендыСпр.Дата = &ДоговорДата
		|	И ДоговорыАрендыСпр.Номер = &ДоговорНомер
		|");
	
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("Контрагент", Параметры.Контрагент);
	Запрос.УстановитьПараметр("ДоговорНомер", Параметры.ДоговорНомер);
	Запрос.УстановитьПараметр("ДоговорДата", Параметры.ДоговорДата);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		НовыйЭлемент = Справочники.ДоговорыАренды.СоздатьЭлемент();
		НовыйЭлемент.Дата = Параметры.ДоговорДата;
		НовыйЭлемент.Номер = Параметры.ДоговорНомер;
		НовыйЭлемент.Организация = Параметры.Организация;
		НовыйЭлемент.Контрагент = Параметры.Контрагент;
		НовыйЭлемент.Наименование = Параметры.ДоговорНаименование;
		
		Попытка
			
			НовыйЭлемент.Записать();
			Возврат НовыйЭлемент.Ссылка;
			
		Исключение
			
			ЗаписьЖурналаРегистрации("Создание элемента справочника Договоры аренды фоновым заданием Получить заказ наряды", 
				УровеньЖурналаРегистрации.Ошибка,,
				НовыйЭлемент,
				ОписаниеОшибки());
				
			Возврат Справочники.ДоговорыАренды.ПустаяСсылка();
			
		КонецПопытки;
		
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Договор;
	КонецЦикла;
	// } Михалев Д.А. #00001#17.01.2026#

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметрыОтбора()

	// { Михалев Д.А. #00001#17.01.2026#
	ДатаОкончания = НачалоДня(ТекущаяДатаСеанса()) - 1;
	ДатаНачала = НачалоДня(ДатаОкончания);
	
	//// { Михалев Д.А. #00001#18.01.2026#
	///	[Тестирование фонового задания]
	//ПериодДней = 15;
	//ДатаОкончания = КонецДня(Дата(2026, 1, ПериодДней));
	//РасчетныйДень = ДатаОкончания - (86400 * ПериодДней) + 1;
	//ДатаНачала = НачалоДня(РасчетныйДень);
	//// } Михалев Д.А. #00001#18.01.2026#
	
	ПараметрыОтбора = Новый Структура("ДатаНачала,ДатаОкончания", 
		ДатаНачала, ДатаОкончания);
	
	Возврат ПараметрыОтбора;
	// } Михалев Д.А. #00001#17.01.2026#

КонецФункции

Процедура ВыполнитьЗапросЛизинговыеДвижения(ПараметрыОтбора)

	// { Михалев Д.А. #00001#17.01.2026#
	Подключение = НастройкиИнтеграции.ПолучитьПодключениеCOM("ERP");
	Если Подключение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Подключение.NewObject("Запрос");
	Запрос.TempTablesManager = Подключение.NewObject("МенеджерВременныхТаблиц");
	
	Запрос.Text = ТекстЗапросаЛизинговыеДвижения();
	
	Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыОтбора.ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаНачала", ПараметрыОтбора.ДатаНачала);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.IsEmpty() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Организация = ОрганизацииМодификация.ПолучитьОрганизациюПоИНН(Выборка.ОрганизацияИНН);
		Контрагент = КонтрагентыМодификация.ПолучитьКонтрагентаПоИНН(Выборка.КонтрагентИНН);
		Автомобиль = АвтомобилиМодификация.ПолучитьАвтомобильПоВин(Выборка.ОЭЗаводскойНомер);
		
		ДоговорПараметры = Новый Структура("Организация,Контрагент,ДоговорНомер,ДоговорДата,ДоговорНаименование", 
			Организация, 
			Контрагент, 
			Выборка.ДоговорНомер, 
			Выборка.ДоговорДата, 
			Выборка.ДоговорНаименование);
		
		Договор = ПолучитьДоговор(ДоговорПараметры);
		
		Если Выборка.ВидДвижения = "+" Тогда
			ВидДвижения = Перечисления.ВидДвижения.Приход;
		ИначеЕсли Выборка.ВидДвижения = "-" Тогда
			ВидДвижения = Перечисления.ВидДвижения.Расход;
		КонецЕсли;
		
		НоваяСтруктура = Новый Структура("Организация,Автомобиль,Договор,Период,ДокументНомер,ВидДвижения",
			Организация, 
			Автомобиль, 
			Договор, 
			Выборка.ДокументДата, 
			Выборка.ДокументНомер, 
			ВидДвижения);
		
		НоваяЗапись = РегистрыСведений.ЛизингПоАвтомобилям.СоздатьМенеджерЗаписи();
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, НоваяСтруктура);
		
		НоваяЗапись.Сумма = Выборка.РасчетнаяСтоимость;
		
		Попытка
			
			НоваяЗапись.Записать();
			
		Исключение
			
			ЗаписьЖурналаРегистрации("Создание записи движения лизинговых договоров фоновым заданием Получить лизинговые движения из ERP", 
				УровеньЖурналаРегистрации.Ошибка,,
				НоваяЗапись,
				ОписаниеОшибки());
			
		КонецПопытки;
		
	КонецЦикла;
	// } Михалев Д.А. #00001#17.01.2026#

КонецПроцедуры

Функция ТекстЗапросаЛизинговыеДвижения()

	// { Михалев Д.А. #00001#17.01.2026#
	ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|		ОбъектЭ.Ссылка КАК ОЭ,
		|		ОбъектЭ.ЗаводскойНомер КАК ЗаводскойНомер,
		|		ЕСТЬNULL(АрендованныеОС.Договор, ЕСТЬNULL(ПереданныеВАрендуОС.Договор, НЕОПРЕДЕЛЕНО)) КАК Договор,
		|		ЕСТЬNULL(АрендованныеОС.Договор.Представление, ЕСТЬNULL(ПереданныеВАрендуОС.Договор.Представление, """""""")) КАК ДоговорПредставление,
		|		МестонахождениеОС.Организация КАК Организация
		|		ПОМЕСТИТЬ ВТ_Договора
		|	ИЗ
		|		Справочник.ОбъектыЭксплуатации КАК ОбъектЭ
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(, ДатаИсправления = ДАТАВРЕМЯ(1,1,1)) КАК МестонахождениеОС
		|	
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПереданныеВАрендуОС.СрезПоследних КАК ПереданныеВАрендуОС
		|			ПО МестонахождениеОС.ОсновноеСредство = ПереданныеВАрендуОС.ОсновноеСредство
		|			И МестонахождениеОС.Организация = ПереданныеВАрендуОС.Организация
		|	
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АрендованныеОС.СрезПоследних КАК АрендованныеОС
		|			ПО МестонахождениеОС.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|			И МестонахождениеОС.Организация = АрендованныеОС.Организация
		|			
		|		ПО ОбъектЭ.Ссылка = МестонахождениеОС.ОсновноеСредство
		|	ГДЕ
		|		ОбъектЭ.ПометкаУдаления = ЛОЖЬ
		|		И (НЕ АрендованныеОС.Договор ЕСТЬ NULL
		|			ИЛИ НЕ ПереданныеВАрендуОС.Договор ЕСТЬ NULL)
		|		И ОбъектЭ.ГруппаОС = Значение(Перечисление.ГруппыОС.ТранспортныеСредства)
		|;
		|
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ВТ_Договора.ОЭ) КАК КолОЭ,
		|	ВТ_Договора.Договор КАК Договор,
		|	ВТ_Договора.Организация КАК Организация
		|	ПОМЕСТИТЬ ВТ_ДоговорНаборОЭ
		|ИЗ
		|	ВТ_Договора КАК ВТ_Договора
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Договора.Организация,
		|	ВТ_Договора.Договор
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(ВТ_Договора.ОЭ) > 1
		|	
		|;
		|
		|ВЫБРАТЬ
		|	ВТ_Договора.ЗаводскойНомер КАК ОЭЗаводскойНомер,
		|	ПоступлениеУслуг.Номер КАК ДокументНомер,
		|	ПоступлениеУслуг.Дата КАК ДокументДата,
		|	СУММА(ПоступлениеУслуг.СуммаДокумента) КАК ДокументСумма,
		|	СУММА(ВЫБОР
		|				КОГДА НЕ ВТ_ДоговорНаборОЭ.Договор ЕСТЬ NULL
		|					ТОГДА ПоступлениеУслуг.СуммаДокумента / ВТ_ДоговорНаборОЭ.КолОЭ
		|				ИНАЧЕ ПоступлениеУслуг.СуммаДокумента
		|			КОНЕЦ) КАК РасчетнаяСтоимость,
		|	""+"" КАК ВидДвижения,
		|	ВТ_Договора.ДоговорПредставление КАК ДоговорНаименование,
		|	ДоговорАрендыСпр.Номер КАК ДоговорНомер,
		|	ДоговорАрендыСпр.Дата КАК ДоговорДата,
		|	ОрганизацияСпр.ИНН КАК ОрганизацияИНН,
		|	КонтрагентСпр.ИНН КАК КонтрагентИНН
		|ИЗ
		|	ВТ_Договора КАК ВТ_Договора
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеУслугПоАренде КАК ПоступлениеУслуг
		|	ПО ВТ_Договора.Договор = ПоступлениеУслуг.Договор
		|	И ВТ_Договора.Организация = ПоступлениеУслуг.Организация
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДоговорНаборОЭ КАК ВТ_ДоговорНаборОЭ
		|	ПО ВТ_Договора.Договор = ВТ_ДоговорНаборОЭ.Договор
		|	И ВТ_Договора.Организация = ВТ_ДоговорНаборОЭ.Организация
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацияСпр
		|	ПО ВТ_Договора.Организация = ОрганизацияСпр.Ссылка
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыАренды КАК ДоговорАрендыСпр
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК КонтрагентСпр
		|	ПО ДоговорАрендыСпр.Контрагент = КонтрагентСпр.Ссылка
		|	
		|	ПО ВТ_Договора.Договор = ДоговорАрендыСпр.Ссылка
		|ГДЕ
		|	ПоступлениеУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ПоступлениеУслуг.ПометкаУдаления = ЛОЖЬ
		|	И ПоступлениеУслуг.Проведен = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Договора.ЗаводскойНомер,
		|	ПоступлениеУслуг.Номер,
		|	ПоступлениеУслуг.Дата,
		|	ВТ_Договора.ДоговорПредставление,
		|	ДоговорАрендыСпр.Номер,
		|	ДоговорАрендыСпр.Дата,
		|	ОрганизацияСпр.ИНН,
		|	КонтрагентСпр.ИНН
		|
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	ВТ_Договора.ЗаводскойНомер,
		|	СписаниеБДС.Номер,
		|	СписаниеБДС.Дата,
		|	СУММА(СписаниеБДСРасшифровка.СуммаВзаиморасчетов),
		|	СУММА(ВЫБОР
		|				КОГДА НЕ ВТ_ДоговорНаборОЭ.Договор ЕСТЬ NULL
		|					ТОГДА СписаниеБДСРасшифровка.СуммаВзаиморасчетов / ВТ_ДоговорНаборОЭ.КолОЭ
		|				ИНАЧЕ СписаниеБДСРасшифровка.СуммаВзаиморасчетов
		|			КОНЕЦ),
		|	""-"",
		|	ВТ_Договора.ДоговорПредставление,
		|	ДоговорАрендыСпр.Номер,
		|	ДоговорАрендыСпр.Дата,
		|	ОрганизацияСпр.ИНН,
		|	КонтрагентСпр.ИНН
		|ИЗ
		|	ВТ_Договора КАК ВТ_Договора
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеБДСРасшифровка
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБДС
		|	ПО СписаниеБДСРасшифровка.Ссылка = СписаниеБДС.Ссылка
		|	
		|	ПО ВТ_Договора.Договор = СписаниеБДСРасшифровка.ДоговорАренды
		|	И ВТ_Договора.Организация = СписаниеБДСРасшифровка.Ссылка.Организация
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДоговорНаборОЭ КАК ВТ_ДоговорНаборОЭ
		|	ПО ВТ_Договора.Договор = ВТ_ДоговорНаборОЭ.Договор
		|	И ВТ_Договора.Организация = ВТ_ДоговорНаборОЭ.Организация
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацияСпр
		|	ПО ВТ_Договора.Организация = ОрганизацияСпр.Ссылка
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыАренды КАК ДоговорАрендыСпр
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК КонтрагентСпр
		|	ПО ДоговорАрендыСпр.Контрагент = КонтрагентСпр.Ссылка
		|	
		|	ПО ВТ_Договора.Договор = ДоговорАрендыСпр.Ссылка
		|ГДЕ
		|	СписаниеБДС.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И СписаниеБДС.ПометкаУдаления = ЛОЖЬ
		|	И СписаниеБДС.Проведен = Истина
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Договора.ЗаводскойНомер,
		|	СписаниеБДС.Номер,
		|	СписаниеБДС.Дата,
		|	ВТ_Договора.ДоговорПредставление,
		|	ДоговорАрендыСпр.Номер,
		|	ДоговорАрендыСпр.Дата,
		|	ОрганизацияСпр.ИНН,
		|	КонтрагентСпр.ИНН
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументДата
		|";
	
	Возврат ТекстЗапроса;
	// } Михалев Д.А. #00001#17.01.2026#

КонецФункции

#КонецОбласти
